#lang rhombus

check:
  10 * (-3) + 2
  ~is -28

check:
  10 + (-3) * 2
  ~is 4

check:
  10 * 2 / 4
  ~is 5

check:
  ~eval
  10 / 2 * 4
  ~is 20

check:
  fun five(x):
    5
  3*five(#true && #false || 2 < 3)-2
  ~is 13

check: 3 ** 4 ~is 3*3*3*3
check: 2 ** 3 ** 2 ~is 2 ** (3 ** 2)
check: 2 ** 3 ** 2 == (2 ** 3) ** 2 ~is #false
check:
  2**2 + 2**2 - (2**2 * 2**2) div 2**2
  ~is 4 + 4 - (4 * 4) div 4
check:
  [2**2 / 2**2 , 2**2 * 2**2 , 2**2 mod 2**2]
  ~is [4/4, 4*4, 4 mod 4]
check:
  2**2 <= 2**2 && 2**2 > 2**2 || 2**2 == 2**2
  ~is 4 <= 4 && 4 > 4 || 4 == 4

macro 'check_non_assoc:
         $op ...
         ...':
  'block:
     check:
       ~eval
       1 $op 2 $op 3
       ~throws "found operators at same precedence, but both are non-associative"
     ...
     ...'

check_non_assoc:
  == != ===
  is_now is_same_number_or_object
  < <= compares_equal compares_unequal >= >
  .< .<= .= .!= .>= .>
